# x264 和 openh264 汇编比较

# 2024-05-03 Lighthawk

## 参考资料

> x264源码：version 164
>
> openh264源码：version 2.3.1

## 基线内容

> CPU：
>
> Memory：

## 测试一：satd 4x4

- #### 这里的差异肉眼可以看出来了，x264汇编专门针对64bit进行了优化，其 32bit 的汇编跟 openh264 对应接口的是一样的

> x264：
>
> 编译宏：X264_CPU_NEON
>
> 测试汇编函数：pixel_satd_4x4_neon

```c
// common/aarch64/pixel.h
#define x264_pixel_satd_4x4_neon x264_template(pixel_satd_4x4_neon)

// common/pixel.c
#if HAVE_AARCH64
    if( cpu&X264_CPU_NEON ) {
        pixf->satd[PIXEL_4x4] = x264_pixel_satd_4x4_neon;
    }
#endif // HAVE_AARCH64
```

```assembly
# common/aarch64/pixel-a.S
# pixel_satd_4x4_neon 在该文件下有两种实现，应该对应两种情况... 还没找到条件上的差异

# 第一处实现：L378
function pixel_satd_4x4_neon, export=1
    ld1         {v1.s}[0],  [x2], x3
    ld1         {v0.s}[0],  [x0], x1
    ld1         {v3.s}[0],  [x2], x3
    ld1         {v2.s}[0],  [x0], x1
    ld1         {v1.s}[1],  [x2], x3
    ld1         {v0.s}[1],  [x0], x1
    ld1         {v3.s}[1],  [x2], x3
    ld1         {v2.s}[1],  [x0], x1

    usubl       v0.8h,  v0.8b,  v1.8b
    usubl       v1.8h,  v2.8b,  v3.8b
    SUMSUB_AB   v2.8h,  v3.8h,  v0.8h,  v1.8h

    zip1        v0.2d,  v2.2d,  v3.2d
    zip2        v1.2d,  v2.2d,  v3.2d
    SUMSUB_AB   v2.8h,  v3.8h,  v0.8h,  v1.8h

    trn1        v0.8h,  v2.8h,  v3.8h
    trn2        v1.8h,  v2.8h,  v3.8h
    SUMSUB_AB   v2.8h,  v3.8h,  v0.8h,  v1.8h

    trn1        v0.4s,  v2.4s,  v3.4s
    trn2        v1.4s,  v2.4s,  v3.4s
    abs         v0.8h,  v0.8h
    abs         v1.8h,  v1.8h
    umax        v0.8h,  v0.8h,  v1.8h

    uaddlv      s0,  v0.8h
    mov         w0,  v0.s[0]
    ret
endfunc

# 第二处实现：L1741
function pixel_satd_4x4_neon, export=1
    lsl         x1, x1, #1
    lsl         x3, x3, #1
    ld1         {v1.d}[0], [x2], x3
    ld1         {v0.d}[0], [x0], x1
    ld1         {v3.d}[0], [x2], x3
    ld1         {v2.d}[0], [x0], x1
    ld1         {v1.d}[1], [x2], x3
    ld1         {v0.d}[1], [x0], x1
    ld1         {v3.d}[1], [x2], x3
    ld1         {v2.d}[1], [x0], x1

    sub         v0.8h, v0.8h, v1.8h
    sub         v1.8h, v2.8h, v3.8h

    SUMSUB_AB   v2.8h, v3.8h, v0.8h, v1.8h

    zip1        v0.2d, v2.2d, v3.2d
    zip2        v1.2d, v2.2d, v3.2d
    SUMSUB_AB   v2.8h, v3.8h, v0.8h, v1.8h

    trn1        v0.8h, v2.8h, v3.8h
    trn2        v1.8h, v2.8h, v3.8h
    SUMSUB_AB   v2.8h, v3.8h, v0.8h, v1.8h

    trn1        v0.4s, v2.4s, v3.4s
    trn2        v1.4s, v2.4s, v3.4s
    abs         v0.8h, v0.8h
    abs         v1.8h, v1.8h
    umax        v0.8h, v0.8h, v1.8h
    uaddlv      s0, v0.8h
    fmov        w0, s0
    ret
endfunc

# arm 目录下也有该函数
# 实现：L849
function pixel_satd_4x4_neon
    vld1.32     {d1[]},  [r2], r3
    vld1.32     {d0[]},  [r0,:32], r1
    vld1.32     {d3[]},  [r2], r3
    vld1.32     {d2[]},  [r0,:32], r1
    vld1.32     {d1[1]}, [r2], r3
    vld1.32     {d0[1]}, [r0,:32], r1
    vld1.32     {d3[1]}, [r2], r3
    vld1.32     {d2[1]}, [r0,:32], r1
    vsubl.u8    q0,  d0,  d1
    vsubl.u8    q1,  d2,  d3

    SUMSUB_AB   q2, q3, q0, q1
    SUMSUB_ABCD d0, d2, d1, d3, d4, d5, d6, d7
    HADAMARD    1, sumsub, q2, q3, q0, q1
    HADAMARD    2, amax,   q0,,    q2, q3

    HORIZ_ADD   d0,  d0,  d1
    vmov.32     r0,  d0[0]
    bx          lr
endfunc
```

> openh264：
>
> 编译宏：HAVE_NEON
>
> 测试汇编函数：WelsSampleSatd4x4_neon

```c
// codec/encoder/core/inc/sample.h
#if defined (HAVE_NEON)
int32_t WelsSampleSatd4x4_neon (uint8_t*, int32_t, uint8_t*, int32_t);
#endif
```

```assembly
# codec/encoder/core/arm/pixel_neon.S
# O.O 这里目录是 arm，不是 arm64
WELS_ASM_FUNC_BEGIN WelsSampleSatd4x4_neon

    //Load the pix1 data --- 16 bytes
    vld1.32  {d0[0]}, [r0], r1
    vld1.32  {d0[1]}, [r0], r1
    vld1.32  {d1[0]}, [r0], r1
    vld1.32  {d1[1]}, [r0]

    //Load the pix2 data --- 16 bytes
    vld1.32  {d2[0]}, [r2], r3
    vld1.32  {d2[1]}, [r2], r3
    vld1.32  {d3[0]}, [r2], r3
    vld1.32  {d3[1]}, [r2]

    //Get the difference
    vsubl.u8 q15, d0, d2 //{0,1,2,3,4,5,6,7}
    vsubl.u8 q14, d1, d3 //{8,9,10,11,12,13,14,15}

    //Do the vertical transform
    vadd.s16 q13, q15, q14 //{0,4,8,12,1,5,9,13}
    vsub.s16 q12, q15, q14 //{2,6,10,14,3,7,11,15}
    vswp  d27, d24
    vadd.s16 q15, q13, q12 //{0,1,2,3,4,5,6,7}
    vsub.s16 q14, q13, q12 //{12,13,14,15,8,9,10,11}

    //Do the horizontal transform
    vtrn.32 q15, q14
    vadd.s16 q13, q15, q14
    vsub.s16 q12, q15, q14

    vtrn.16 q13, q12
    vadd.s16 q15, q13, q12

    //Do the SAD
    vabs.s16 q15, q15
    vabd.s16 q14, q13, q12

    vadd.u16 q0, q15, q14

    vrhadd.u16 d0, d1
    vpaddl.u16 d0, d0
    vpaddl.u32 d0, d0

    vmov.u32   r0, d0[0]

WELS_ASM_FUNC_END
```

- #### 搭建环境进行测试

## 测试二：？

- ### 想找个汇编多发的看下差异...找之中 
